plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'maven-publish'
    id 'jacoco'
}

group 'edu.austral.ingsis.prinstcript'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    google()
    mavenLocal()
}
ext {
    junitVersion = '5.9.1'
}

sourceCompatibility = '15'
targetCompatibility = '15'

[compileKotlin, compileTestKotlin].forEach {
    it.kotlinOptions {
        jvmTarget = '15'
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.0-Beta")
    implementation 'com.google.code.gson:gson:2.10.1'
}
check.dependsOn 'ktlintFormat'

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
jacoco {
    toolVersion = "0.8.8"
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0 // TODO change this
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Nachoferra1894/printscript")
            credentials {
                username = project.findProperty("GITHUB_ACTOR") as String ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("GITHUB_TOKEN") as String ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

